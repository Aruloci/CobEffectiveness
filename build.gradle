plugins {
    id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
//    id 'org.jetbrains.kotlin.jvm' version '1.9.23' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version

    // Kotlin plugin and Java for all subprojects
    apply plugin: 'java'
//    apply plugin: 'org.jetbrains.kotlin.jvm'

    java {
        withSourcesJar()
    }

    configurations.configureEach {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-stdlib:2.0.20",
                    "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.20",
                    "org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.0.20",
                    "org.jetbrains.kotlin:kotlin-reflect:2.0.20"
        }
    }

    repositories {
        maven { url = "https://cursemaven.com" }
        maven { url = "https://maven.fabricmc.net" }
        maven {
            url = "https://api.modrinth.com/maven"
            content { includeGroup "maven.modrinth" }
        }
        mavenCentral()
        maven { url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/" }
        maven { url = "https://maven.impactdev.net/repository/development/" }
        maven { url = "https://maven.neoforged.net/releases" }
        maven { url = "https://thedarkcolour.github.io/KotlinForForge/" }

    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        archivesName = "$rootProject.archives_name-$project.name"
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"

        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
            it.mappings("dev.architectury:yarn-mappings-patch-neoforge:${rootProject.yarn_mappings_patch_neoforge_version}")
        }
    }

    // Java 21
    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    tasks.withType(JavaCompile).configureEach {
        options.release = 21
    }

    // publishing boilerplate
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }
        repositories {
            // publish target
        }
    }
}
